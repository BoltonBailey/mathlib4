/-
Copyright (c) 2025 Bolton Bailey. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bolton Bailey
-/
module

public import Mathlib.Analysis.Convex.Combination
public import Mathlib.Analysis.Convex.SimplicialComplex.Basic
public import Mathlib.LinearAlgebra.Finsupp.VectorSpace
public import Mathlib.Combinatorics.SimpleGraph.Basic

/-!
# Simplicial complexes from affinely independent points

This file provides constructions for simplicial complexes where the vertices
are affinely independent.

## Main declarations

* `Geometry.SimplicialComplex.ofAffineIndependent`: Construct a simplicial complex from a
  downward-closed set of faces whose union of vertices is affinely independent.
* `Geometry.SimplicialComplex.onFinsupp`: Construct a simplicial complex on `Î¹ â†’â‚€ ğ•œ` from a
  downward-closed set of finite subsets of `Î¹`, using the standard basis vectors.
* `Geometry.SimplicialComplex.ofSimpleGraph`: Construct a simplicial complex from a simple graph,
  where vertices of the graph are 0-simplices and edges are 1-simplices.
-/

@[expose] public section

open Finset Set

namespace Geometry

namespace AbstractSimplicialComplex

/--
Construct an abstract simplicial complex from a simple graph, where vertices of the graph
are 0-simplices and edges are 1-simplices.
-/
def ofSimpleGraph {Î¹ : Type*} [DecidableEq Î¹] (G : SimpleGraph Î¹) :
    AbstractSimplicialComplex Î¹ where
  faces := ({s : Finset Î¹ | âˆƒ v, s = {v}}) âˆª Sym2.toFinset '' G.edgeSet
  isRelLowerSet_faces := by
    intro s hs
    simp only [Set.mem_union, Set.mem_setOf_eq, Set.mem_image] at hs
    rcases hs with âŸ¨v, rflâŸ© | âŸ¨e, he, rflâŸ©
    Â· simp
    Â· constructor
      Â· exact Finset.nonempty_iff_ne_empty.mpr (Sym2.toFinset_ne_empty e)
      Â· intro b hb_sub hb_nonempty
        by_cases h : b.card = 1 <;>
          grind [Finset.card_eq_one, Finset.eq_of_subset_of_card_le hb_sub, Sym2.card_toFinset]
  singleton_mem := by
    simp only [Set.mem_union, Set.mem_setOf_eq, Set.mem_image]
    intro v
    exact Or.inl âŸ¨v, rflâŸ©

end AbstractSimplicialComplex

namespace SimplicialComplex

/--
Construct a simplicial complex from a `PreAbstractSimplicialComplex` on a set of points in a space,
under the assumption that the union of the defining points is affinely independent.
-/
def ofAffineIndependent {ğ•œ E}
    [Field ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ] [DecidableEq E] [AddCommGroup E] [Module ğ•œ E]
    (abstract : PreAbstractSimplicialComplex E)
    (indep : AffineIndependent ğ•œ (Subtype.val : (â‹ƒ s âˆˆ abstract.faces, (s : Set E)) â†’ E)) :
    SimplicialComplex ğ•œ E where
  toPreAbstractSimplicialComplex := abstract
  indep {s} hs := indep.mono (Set.subset_biUnion_of_mem hs)
  inter_subset_convexHull {s t} hs ht := by
    apply subset_of_eq
    rw [AffineIndependent.convexHull_inter (R := ğ•œ) (s := s âˆª t)]
    Â· apply indep.mono
      simp only [Finset.coe_union]
      exact Set.union_subset (Set.subset_biUnion_of_mem hs) (Set.subset_biUnion_of_mem ht)
    Â· exact Finset.subset_union_left
    Â· exact Finset.subset_union_right

/--
Construct a simplicial complex from an abstract simplicial complex on a set of points
over the `ğ•œ`-module of finitely supported functions on those points.
-/
noncomputable def onFinsupp {ğ•œ Î¹ : Type*} [DecidableEq Î¹]
    [DecidableEq ğ•œ] [Field ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ]
    (abstract : PreAbstractSimplicialComplex Î¹) :
    SimplicialComplex ğ•œ (Î¹ â†’â‚€ ğ•œ) :=
  ofAffineIndependent (ğ•œ := ğ•œ) (E := Î¹ â†’â‚€ ğ•œ)
    (abstract.map (fun i => Finsupp.single i (1 : ğ•œ)))
    (by
      refine (Finsupp.linearIndependent_single_one ğ•œ Î¹).affineIndependent.range.mono fun x hx => ?_
      simp only [Set.mem_iUnion, Finset.mem_coe] at hx
      obtain âŸ¨_, âŸ¨_, _, rflâŸ©, hxâŸ© := hx
      grind )

/--
The simplicial complex associated to a simple graph, where vertices of the graph
are 0-simplices and edges are 1-simplices. The complex is constructed over the
`ğ•œ`-module of finitely supported functions on the vertex type.
-/
noncomputable def ofSimpleGraph {ğ•œ V : Type*} [DecidableEq V] [DecidableEq ğ•œ]
    [Field ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ]
    (G : SimpleGraph V) :
    SimplicialComplex ğ•œ (V â†’â‚€ ğ•œ) :=
  onFinsupp (AbstractSimplicialComplex.ofSimpleGraph G).toPreAbstractSimplicialComplex

end SimplicialComplex

end Geometry
